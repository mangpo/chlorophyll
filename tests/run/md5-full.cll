// 2**16-1 = 65535
// 2**32-1 = 4294967295

int::2@(17, 29) leftrotate(int::2@(17,17) x, int@11 r) {
  int::2@(17, 29) out;
  int@17 swap;
  if(r >@11 16) {
    swap = x::0;
    x::0 = x::1;
    x::1 = swap;
    r = r - 16;
  }
  out::0 = ((x::1 >>@17 (16 -@17 r)) |@17 (x::0 <<@17 r)) &@17 65535;
  out::1 = ((x::1 <<@17 r) |@17 (x::0 >>@17 (16 -@17 r))) &@29 65535;
  return out;
}

int::2@(28, 29) csum(int::2@(28, 29) x, int::2@(28, 29) y) {
  int::2@(28, 29) z;
  z::0 = x::0 +@28 y::0;
  z::1 = x::1 +@29 y::1 +@29 (z::0 >>@28 16);
  z::0 = z::0 &@28 65535;
  z::1 = z::1 &@29 65535;
  return z;
}

int::2@(10, 25) cadd(int::2@(24,25) x, int::2@(24,25) y) {
  int::2@(24,25) z;
  z::0 = x::0 +@24 y::0;
  z::1 = x::1 +@25 y::1 +@25 (z::0 >>@24 16);
  z::0 = z::0 &@24 65535;
  z::1 = z::1 &@25 65535;
  return z;
}

int::2@(0,1) sumrotate(int::2@(0,1) a, int::2@(0,1) b, int::2@(0,1) f, int::2@(2,3) k, int::2 m, int@6 r) {
  return csum(b, leftrotate(csum(a +@(0,1) f +@(0,1) k, m), r));
}

void main() {
  int::2[]@({[0:16]=2,[16:32]=7,[32:64]=12},{[0:16]=3,[16:32]=8,[32:64]=13}) k[64]; // full = 64 entries
  int[]@{[0:16]=6,[16:32]=16,[32:48]=18,[48:64]=19} r[64];
  
  int::2@(20,21) a;
  int::2@(14,15) b;
  int::2@(26,27) c;
  int::2@(22,23) d;
  int::2@(30,31) f1;
  int::2@(32,33) f2;
  int::2@(0,1) temp;
  int::2[]@({[0:4]=10},{[0:4]=9}) hash[4];
  
  int::2[]@({[0:16]=4},{[0:16]=5}) message[16];
  int::2@(4,5) g;
  int::2@(4,5) inc;

  for(i from 0 to 64) {
    k::1[i] = in();
    k::0[i] = in();
  }
  
  for(i from 0 to 64) {
    r[i] = in();
  }

  for(i from 0 to 4) {
    hash::1[i] = in();
    hash::0[i] = in();
  }

  for(i from 0 to 16) {
    message::1[i] = in();
    message::0[i] = in();
  }
  
  for(t from 0 to 16) {
    a = hash[0];
    b = hash[1];
    c = hash[2];
    d = hash[3];
    g = 0;
    inc = 1;
    for(i from 0 to 16) { 
      f1 = (b &@(30,31) c) |@(30,31) ((b ^@(30,31) 65535) &@(30,31) d);
      temp = sumrotate(a,b,f1,k[i],message[g],r[i]);
	    a = d;
	    d = c;
	    c = b;
	    b = temp;
      g = (g +@(4,5) inc) &@(4,5) 15;
    }
    g = 1;
    inc = 5;
    for(i from 16 to 32) { 
      f1 = (d &@(30,31) b) |@(30,31) ((d ^@(30,31) 65535) &@(30,31) c);
      temp = sumrotate(a,b,f1,k[i],message[g],r[i]);
	    a = d;
	    d = c;
	    c = b;
	    b = temp;
      g = (g +@(4,5) inc) &@(4,5) 15;
    }
    g = 5;
    inc = 3;
    for(i from 32 to 48) { 
      f2 = b ^@(32,33) c ^@(32,33) d;
      temp = sumrotate(a,b,f2,k[i],message[g],r[i]);
	    a = d;
	    d = c;
	    c = b;
	    b = temp;
      g = (g +@(4,5) inc) &@(4,5) 15;
    }
    g = 0;
    inc = 7;
    for(i from 48 to 64) { 
      f2 = c ^@(32,33) (b |@(32,33) (d ^@(32,33) 65535));
      temp = sumrotate(a,b,f2,k[i],message[g],r[i]);
	    a = d;
	    d = c;
	    c = b;
	    b = temp;
      g = (g +@(4,5) inc) &@(4,5) 15;
    }
    hash[0] = cadd(hash[0],a);
    hash[1] = cadd(hash[1],b);
    hash[2] = cadd(hash[2],c);
    hash[3] = cadd(hash[3],d);
  } 

  for(i from 0 to 4) {
    out(hash::1[i]);
    out(hash::0[i]);
  }
}

/*
core = 0, space = 310, ops = #<set:>
core = 1, space = 306, ops = #<set:>
core = 2, space = 262, ops = #<set:>
core = 3, space = 262, ops = #<set:>
core = 4, space = 260, ops = #<set:>
core = 5, space = 260, ops = #<set:>
core = 6, space = 262, ops = #<set:>
core = 7, space = 202, ops = #<set:>
core = 8, space = 202, ops = #<set:>
core = 9, space = 262, ops = #<set:>
core = 10, space = 266, ops = #<set:>
core = 11, space = 288, ops = #<set:>
core = 12, space = 266, ops = #<set:>
core = 13, space = 266, ops = #<set:>
core = 14, space = 0, ops = #<set:>
core = 15, space = 0, ops = #<set:>
core = 16, space = 202, ops = #<set:>
core = 17, space = 284, ops = #<set:>
core = 18, space = 202, ops = #<set:>
core = 19, space = 202, ops = #<set:>
*/

/* new estimate
core = 0, space = 1338, ops = #<set:>
core = 1, space = 1315, ops = #<set:>
core = 2, space = 459, ops = #<set:>
core = 3, space = 459, ops = #<set:>
core = 4, space = 513, ops = #<set:>
core = 5, space = 513, ops = #<set:>
core = 6, space = 427, ops = #<set:>
core = 7, space = 326, ops = #<set:>
core = 8, space = 326, ops = #<set:>
core = 9, space = 684, ops = #<set:>
core = 10, space = 707, ops = #<set:>
core = 11, space = 482, ops = #<set:>
core = 12, space = 390, ops = #<set:>
core = 13, space = 390, ops = #<set:>
core = 14, space = 0, ops = #<set:>
core = 15, space = 0, ops = #<set:>
core = 16, space = 326, ops = #<set:>
core = 17, space = 526, ops = #<set:>
core = 18, space = 326, ops = #<set:>
core = 19, space = 326, ops = #<set:>
*/
